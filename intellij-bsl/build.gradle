plugins {
    id "org.jetbrains.intellij" version "0.3.4"
    id "jacoco"
    id "com.github.hierynomus.license"
}

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
    maven { url 'https://maven.atlassian.com/3rdparty' }
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

group 'org.github._1c_syntax.intellij.bsl'
version '1.0' // Plugin version

dependencies {
    compile project(':languageserver')
    compile project(':bslparser')
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', 'vendor/adaptor/src']
        resources.srcDirs = ['src/main/resources']
    }
}

intellij {
    version 'IC-2017.3' //Corresponds to 173. branch from plugin.xml; for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName 'Language 1C (BSL)'
    plugins = ['com.github.gtache.lsp:1.4.0']
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

test {
    jacoco {
        append = false
    }
}

license {
    header = rootProject.file("license/HEADER.txt")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Alexey Sosnoviy <labotamy@yandex.ru>, Nikita Gryzlov <nixel2007@gmail.com>'
    ext.project = "IntelliJ Language 1C (BSL) Plugin"
    strictCheck = true
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}

runIde {
    systemProperty "idea.plugins.path", properties.sandboxDirectory
}
