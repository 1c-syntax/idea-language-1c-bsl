plugins {
    id "org.jetbrains.intellij" version "0.3.12"
    id "jacoco"
    id "idea"
    id "java"
    id "com.github.hierynomus.license"
}

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
    maven {
        url "https://dl.bintray.com/antlr/maven/"
    }
}

group 'org.github._1c_syntax.intellij.bsl'
version '1.0' // Plugin version

dependencies {
    compile project(':languageserver')
    compile project(':bslparser')
    compile ('org.antlr:antlr4-jetbrains-adapter:2.1.0') {
        exclude group: 'com.jetbrains'
    }
}

intellij {
    version 'IC-2018.3' //Corresponds to 183.4284.85 from plugin.xml; for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName 'Language 1C (BSL)'
    plugins = ['com.github.gtache.lsp:1.4.0']
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

test {
    jacoco {
        append = false
    }
}

license {
    header = rootProject.file("license/HEADER.txt")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Alexey Sosnoviy <labotamy@yandex.ru>, Nikita Gryzlov <nixel2007@gmail.com>'
    ext.project = "IntelliJ Language 1C (BSL) Plugin"
    strictCheck = true
    exclude "org/antlr/jetbrains/adaptor/**"
    exclude "**/*.txt"
    exclude "**/*.xml"
    mapping "java", "SLASHSTAR_STYLE"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}

runIde {
    systemProperty "idea.plugins.path", properties.sandboxDirectory
}
